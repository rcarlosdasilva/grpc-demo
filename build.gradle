buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE'
    }
}

allprojects {
    group 'demo'
    version '1.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:Cairo-SR6'
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Finchley.SR2'
        }
    }
}

extensions['guava.version'] = '26.0-jre'

subprojects {
    apply plugin: 'idea'

    dependencies {
        compile 'com.google.guava:guava'

        testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.1'
        testCompile 'org.hamcrest:hamcrest-core:1.3'
        testCompile 'org.hamcrest:hamcrest-library:1.3'
    }

    task packageSources(type: Jar) {
        classifier = 'sources'
        from(sourceSets.main.allSource)
    }

    artifacts.archives(tasks.packageSources)

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}
